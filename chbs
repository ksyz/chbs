#!/usr/bin/perl
# 2015, Michal Ingeli <mi@v3.sk>, WTF-PL
package Acme::CHBS;

use strict;
use warnings;
use Data::Dumper;

use Math::Random::Secure qw(irand);
use Getopt::Long  qw(GetOptionsFromArray :config no_ignore_case bundling);
use File::Slurp;

sub check_word;
sub check_passphrase;
sub mangle;
sub dice;
sub jar;
sub throw;
sub shuffle;
sub help;

__PACKAGE__->main(@ARGV) unless caller;

sub main {
	my @ARGV = @_;

	my %c = ( count => 1, );
	my $options = GetOptionsFromArray(\@ARGV,
		'w|words=i' => \$c{words},
		'S|symbols' => \$c{symbols},
		'm|min=i' => \$c{min_length},
		'M|max=i' => \$c{max_length},
		'd|dictionary=s' => \$c{dict},
		'c|count=i' => \$c{count},
		'h|help' => \$c{help},
		'C|capitalize-first' => \$c{capitalize_first},
		'l|min-length=i' => \$c{passphrase_min_length},
		'L|max-length=i' => \$c{passphrase_max_length},
	);
	exit 1
		unless ($options);

	if ($c{help}) {
		print help;
		exit 0;
	}

	local $\ = "\n";
	my $chbs = __PACKAGE__->new(map { $_ => $c{$_} } grep { defined $c{$_} } keys %c);
	for (;$c{count};$c{count}--) {
		print $chbs->shuffle;
	}
}

sub new {
	my $class = shift;
	my $self = {
		words => 6,
		symbols => 0,
		min_length => 4,
		max_length => 8,
		dict => '/usr/share/dict/words',
		capitalize_first => 0,
		passphrase_min_length => 1,
		passphrase_max_length => 32,
		@_ 
	};

	$self = bless $self, $class;

	my @lines = read_file($self->{dict});
	$self->{_lines} = [@lines];
	return $self;
};

sub help {
	return <<HELP;
cbhs [ OPTIONS ]
OPTIONS:
-w,--words INT
	Number of words (default 6)
-S,--symbols
	Don't skip dictionary words with symbols and numbers (default false)
-m,--minimum INT
	Minimum word length (default 4)
-M,--maximum INT
	Maximum word length (default 8)
-d,--dictionary FILE
	Dictionary file (default /usr/share/dict/words)
-c,--count INT
	Number of generated sets, one per line (default one), INT<0 for infinite.
-C,capitalize-first
	Capitalize first letter (default false)
-l,--min-length INT
	Minimum passphrase length (default 1)
-L,--max-length INT
	Maximum passphrase length (default 32)
HELP
};

sub jar {
	my $self = shift;
	my @parts;
	for (my $i = $self->{words}; $i; $i--) {
		push @parts, $self->dice;
	};
	return @parts;
};

sub dice {
	my $self = shift;
	my $word;
	while (1) {
		$word = $self->mangle($self->{_lines}[irand($#{$self->{_lines}})]);
		next 
			unless $self->check_word($word);
		return $word;
	}
};

sub mangle {
	my ($self, $word) = @_;
	$word = lc $word;
	chomp $word;
	$word = ucfirst $word
		if ($self->{capitalize_first});
	return $word;
};
	
sub check_word {
	my ($self, $word) = @_;
	return undef
		if (length $word < $self->{min_length});
	return undef
		if (length $word > $self->{max_length});
	return undef
		if (!$self->{symbols} && $word !~ /^[a-z]+$/i);
	return 1;
};

sub check_passphrase {
	my ($self, $passphrase) = @_;
	return undef
		if (defined $self->{passphrase_min_length} && length $passphrase < $self->{passphrase_min_length});
	return undef
		if (defined $self->{passphrase_max_length} && length $passphrase > $self->{passphrase_max_length});
	return 1;
};

sub shuffle {
	my ($self) = @_;
	while (1) {
		my $try = $self->throw($self->jar);
		next 
			unless $self->check_passphrase($try);
		return $try;
	}
};

sub throw {
	my ($self, @parts) = @_;
	return join(' ', @parts);
};

1;
